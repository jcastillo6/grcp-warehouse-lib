plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.google.protobuf" version "0.9.4"
}

group = 'com.jcastillo6'
version = '0.0.6'

java {
    sourceCompatibility = '17'
    withSourcesJar()
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

def grpcVersion = '1.61.0'
dependencies {
    compileOnly "io.grpc:grpc-protobuf:${grpcVersion}"
    compileOnly "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "io.grpc:grpc-netty:${grpcVersion}"
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

tasks.named('test') {
    useJUnitPlatform()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.15.8"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.36.1"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        proto {
            // In addition to the default "src/main/proto"
            srcDir "src/main/grpc"
            srcDir "src/main/java"
        }
    }
}

sourcesJar {
    dependsOn classes
    archiveClassifier = "sources"
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'warehouse-gateway-api'
            from components.java
        }
    }
}